plugins {
	id "java"
	id "edu.wpi.first.GradleRIO" version "2022.4.1"
	id 'com.diffplug.spotless' version '6.11.0'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

dependencies {
	implementation wpi.java.deps.wpilib()
	implementation wpi.java.vendor.java()
	implementation group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'

	testImplementation 'junit:junit:4.13.2'
}

test {
	testLogging.showStandardStreams = true
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId 'frc.team997.lib'
			artifactId 'spartanLib2'
			version '1.0-SNAPSHOT'

			from components.java
		}
	}
}

spotless {
	format 'misc', {
		target '*.gradle', '*.md', '.gitignore'

		trimTrailingWhitespace()
		indentWithTabs()
		endWithNewline()
	}
	java {
		googleJavaFormat('1.15.0').aosp().reflowLongStrings()
		formatAnnotations()

		licenseHeaderFile 'licenseHeader.txt'
	}
}

tasks.register('updateGitHooks', Copy) {
	from new File(rootProject.rootDir, 'scripts/pre-commit')
	into { new File(rootProject.rootDir, '.git/hooks') }
	fileMode 0775
}
compileJava.dependsOn updateGitHooks

tasks.register('buildDocs', Javadoc) {
	source = sourceSets.main.allJava
	classpath = sourceSets.main.runtimeClasspath
}

repositories {
	mavenLocal()
	mavenCentral()
}
